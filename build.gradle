group 'io.multiplatform'
version '2.3.0-rev1'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.0-rc-116'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'kotlin-multiplatform'

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm') {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                jvmTarget = "1.8"
            }
        }
        fromPreset(presets.js, 'js') {
            tasks.getByName(compilations.main.compileKotlinTaskName).kotlinOptions {
                sourceMap = true
                sourceMapEmbedSources = "always"
                moduleKind = "commonjs"
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib"
            }
        }
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }
    }
}

dependencies {
    jvmMainApi("io.reactivex.rxjava2:rxkotlin:2.3.0") {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }
}

task copyPackageJson(type: Copy) {
    from file("package.json")
    into file("${buildDir}/node_module")
}

task copyJS(type: Copy) {
    from file("${buildDir}/classes/kotlin/js/main/${project.name}.js")
    into file("${buildDir}/node_module")
    rename "${project.name}\\.js", 'index.js'
}

task publishToNpm(type:Exec, dependsOn: [copyPackageJson, copyJS]) {
    workingDir "${buildDir}/node_module"
    executable 'npm'
    args ['publish']
}